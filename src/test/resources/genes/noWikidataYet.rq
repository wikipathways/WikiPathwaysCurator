prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix dc:      <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix xsd:     <http://www.w3.org/2001/XMLSchema#>
prefix wp:      <http://vocabularies.wikipathways.org/wp#>
prefix foaf:    <http://xmlns.com/foaf/0.1/>

SELECT DISTINCT ?homepage (STR(SUBSTR(STR(?type),41)) AS ?wpType)
  (STR(?labelLit) AS ?label) (STR(?identifierLit) AS ?identifier)
WHERE {
  VALUES ?type { wp:GeneProduct wp:Protein }
  ?gene a ?type ;
    dc:source "Wikidata"^^xsd:string ;
    rdfs:label ?labelLit ;
    dcterms:identifier ?identifierLit ;
    dcterms:isPartOf ?pathway .
  ?pathway foaf:page ?homepage .
}
