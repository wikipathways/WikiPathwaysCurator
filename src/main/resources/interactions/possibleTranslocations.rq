prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix gpml:    <http://vocabularies.wikipathways.org/gpml#>
prefix wp:      <http://vocabularies.wikipathways.org/wp#>
prefix foaf:    <http://xmlns.com/foaf/0.1/>

SELECT DISTINCT ?homepage ?interaction ?source ?sourceLabel ?target ?targetLabel
WHERE {
   ?pathway a wp:Pathway ;
            foaf:page ?homepage .
   ?interaction dcterms:isPartOf ?pathway ; 
      a wp:Interaction ;
      wp:target ?target ;
      wp:source ?source .
   ?source a wp:DataNode ;
      rdfs:label ?sourceLabel ;
      wp:bdbWikidata ?sourceQ .
   ?target a wp:DataNode ;
      rdfs:label ?targetLabel ;
      wp:bdbWikidata ?targetQ .
   FILTER (?sourceQ = ?targetQ)
   MINUS { ?interaction a wp:Translocation }
   MINUS { ?interaction a wp:TranscriptionTranslation }
   MINUS { ?source wp:isAbout/^gpml:stateOf [] }
   MINUS { ?target wp:isAbout/^gpml:stateOf [] }
}
