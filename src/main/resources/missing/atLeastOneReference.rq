PREFIX cur: <http://vocabularies.wikipathways.org/wp#Curation:>
PREFIX wp:      <http://vocabularies.wikipathways.org/wp#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT DISTINCT ?homepage (str(?titleLit) as ?title) (str(?speciesLit) as ?species)
WHERE {
  {
    SELECT DISTINCT ?pathway (COUNT(?pubmed) AS ?count) WHERE {
      ?pathway a wp:Pathway .
      OPTIONAL {
        ?pathway cito:cites ?pubmed .
      }
      MINUS { ?pathway wp:ontologyTag cur:Homology_Converted }
    } GROUP BY ?pathway
  }
  ?pathway dc:title ?titleLit ;
           wp:organismName ?speciesLit ;
           foaf:page ?homepage .
  FILTER (?count = 0)
} ORDER BY ?species ?title
