prefix cur: <http://vocabularies.wikipathways.org/wp#Curation:>
prefix wp:      <http://vocabularies.wikipathways.org/wp#>
prefix gpml:    <http://vocabularies.wikipathways.org/gpml#>
prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix xsd:     <http://www.w3.org/2001/XMLSchema#>
prefix foaf:    <http://xmlns.com/foaf/0.1/>

SELECT ?homepage ?node ?datasource WHERE {
  VALUES ?datasource {
    "Wikidata"
    "UniProtKB"
    "Uniprot-TrEMBL"
    "Ensembl"
    "ChEBI"
  }
  ?node a gpml:DataNode ;
        dcterms:isPartOf ?gpmlPathway ;
        gpml:type "Unknown" ;
        gpml:xrefDataSource ?datasource .
  ?pathway foaf:page ?homepage ;
           wp:isAbout ?gpmlPathway .
  MINUS { ?pathway wp:ontologyTag cur:Homology_Converted }
} ORDER BY ?homepage
