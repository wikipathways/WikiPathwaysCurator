prefix rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
prefix dc:      <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix wp:      <http://vocabularies.wikipathways.org/wp#>

SELECT DISTINCT ?url ?interaction
WHERE {
  ?pathway a wp:Pathway ; 
    dc:identifier ?url .
  ?interaction a wp:Conversion ; dcterms:isPartOf ?pathway .
  MINUS { ?interaction wp:target / a wp:Interaction }
  MINUS {
    ?interaction wp:source / a wp:Metabolite ;
                 wp:target / a wp:Metabolite
  }
  MINUS {
    ?interaction wp:source / a wp:Rna ;
                 wp:target / a wp:Rna
  }
}
